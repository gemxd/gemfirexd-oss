sourceSets {
  jca {
    compileClasspath += configurations.compile
    runtimeClasspath += configurations.runtime
  }
}

configurations {
  //declaring new configuration that will be used to associate with artifacts
  archives
}

dependencies {
  provided files("${System.getProperty('java.home')}/../lib/tools.jar")
  provided 'antlr:antlr:2.7.7'
  compile 'commons-io:commons-io:2.4'
  compile 'commons-logging:commons-logging:1.2'
  compile 'commons-lang:commons-lang:2.6'
  compile('commons-modeler:commons-modeler:2.0') {
    exclude(group: 'xml-apis', module: 'xml-apis')
  }
  compile 'commons-cli:commons-cli:1.3.1'
  compile 'javax.mail:javax.mail-api:1.4.5'
  compile 'javax.resource:javax.resource-api:1.7'
  compile 'javax.transaction:javax.transaction-api:1.2'
  compile 'mx4j:mx4j:3.0.2'
  compile 'mx4j:mx4j-remote:3.0.2'
  compile 'mx4j:mx4j-tools:3.0.1'
  provided 'net.sourceforge.jline:jline:1.0.S2-B'
  provided "org.eclipse.jetty:jetty-http:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-io:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-security:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-server:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-util:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
  provided "org.eclipse.jetty:jetty-xml:${jettyVersion}"
  compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
  provided "org.springframework:spring-aop:${springVersion}"
  provided "org.springframework:spring-beans:${springVersion}"
  provided "org.springframework:spring-context:${springVersion}"
  provided "org.springframework:spring-context-support:${springVersion}"
  provided "org.springframework:spring-core:${springVersion}"
  provided "org.springframework:spring-expression:${springVersion}"
  provided "org.springframework:spring-web:${springVersion}"
  provided "org.springframework:spring-webmvc:${springVersion}"
  provided "org.springframework:spring-tx:${springVersion}"
  provided 'org.springframework.shell:spring-shell:1.0.0.RELEASE'
  compile 'org.xerial.snappy:snappy-java:1.1.2.1'

  provided "org.apache.hadoop:hadoop-annotations:${hadoopVersion}"
  provided "org.apache.hadoop:hadoop-auth:${hadoopVersion}"
  provided "org.apache.hadoop:hadoop-common:${hadoopVersion}"
  provided "org.apache.hadoop:hadoop-hdfs:${hadoopVersion}"
  provided "org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}"
  provided "com.google.protobuf:protobuf-java:${protobufVersion}"
  provided "com.sun.jersey:jersey-core:${jerseyVersion}"
  provided "com.sun.jersey:jersey-server:${jerseyVersion}"
  provided "org.mortbay.jetty:jetty:${hadoopJettyVersion}"
  provided "org.mortbay.jetty:jetty-util:${hadoopJettyVersion}"
  provided "com.google.code.findbugs:jsr305:${jsr305Version}"

  compile group: 'org.apache.hbase', name: 'hbase-protocol', version: hbaseVersion
  compile(group: 'org.apache.hbase', name: 'hbase-common', version: hbaseVersion) {
    exclude(group: 'org.apache.hbase', module: 'hbase-annotations')
  }
  compile(group: 'org.apache.hbase', name: 'hbase-client', version: hbaseVersion) {
    exclude(group: 'org.apache.hbase', module: 'hbase-annotations')
    exclude(group: 'io.netty', module: 'netty')
  }
  compile(group: 'org.apache.hbase', name: 'hbase-server', version: hbaseVersion) {
    exclude(group: 'org.apache.hbase', module: 'hbase-annotations')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-core')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-client')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-jobclient')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-core')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-auth')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-annotations')
    exclude(group: 'org.apache.hadoop', module: 'hadoop-hdfs')
    exclude(group: 'org.apache.hbase', module: 'hbase-hadoop1-compat')
    exclude(group: 'commons-math', module: 'commons-math')
    exclude(group: 'org.slf4j', module: 'slf4j-api')
    exclude(group: 'com.sun.jersey', module: 'jersey-server')
    exclude(group: 'com.sun.jersey', module: 'jersey-core')
    exclude(group: 'com.sun.jersey', module: 'jersey-json')
    exclude(group: 'commons-io', module: 'commons-io')
  }
  compile 'org.cloudera.htrace:htrace-core:2.05'

  compile 'com.google.guava:guava:14.0.1'
  compile 'xml-apis:xml-apis:1.4.01'
  compile('xom:xom:1.2.5') {
    exclude(group: 'xalan', module: 'xalan')
    exclude(group: 'xerces', module: 'xercesImpl')
    exclude(group: 'xml-apis', module: 'xml-apis')
  }
  provided 'com.jcraft:jsch:0.1.53'
  provided "org.apache.ant:ant:${antVersion}"

  compile project(subprojectBase + 'gemfire-jgroups')
  compile project(subprojectBase + 'gemfire-joptsimple')
  compile project(subprojectBase + 'gemfire-json')
  compile project(subprojectBase + 'gemfire-shared')

  jcaCompile sourceSets.main.output
}

// Creates the version properties file and writes it to the classes dir
task createVersionPropertiesFile(dependsOn: 'processResources') {
  def propertiesDir = file("${buildDir}/resources/main/com/gemstone/gemfire/internal")
  outputs.file "${propertiesDir}/GemFireVersion.properties"
  inputs.dir compileJava.destinationDir

  doLast {
    def props = [
      "Product-Name"      : PRODUCT_NAME,
      "Product-Version"   : version,
      "Build-Id"          : System.env.USER + ' ' + buildNumber,
      "Build-Date"        : buildDate,
      "Build-Platform"    : "${osName.getName()} ${osVersion} ${osArch}",
      "Build-Java-Version": jdkVersion,
      "Source-Date"       : sourceDate,
      "Source-Revision"   : commitId,
      "Source-Repository" : gitBranch
    ]

    writeProperties(propertiesDir, 'GemFireVersion.properties',
        "Properties that control what version ${GEMFIRE_PRODUCT} will think it is. Changing these values may cause ${GEMFIRE_PRODUCT} to no longer function.", props)
  }
}

jar {
  from sourceSets.main.output
  from sourceSets.jca.output

  exclude 'com/gemstone/gemfire/management/internal/web/**'
  exclude 'com/gemstone/gemfire/internal/i18n/StringIdResourceBundle_ja.txt'
  exclude 'com/gemstone/gemfire/admin/doc-files/ds4_0.dtd'
}

task webJar (type: Jar, dependsOn: classes) {
  description 'Assembles the jar archive containing the gemfire management web classes.'
  from sourceSets.main.output
  baseName 'gemfire-web'
  include 'com/gemstone/gemfire/management/internal/web/**'
}

task raJar (type: Jar, dependsOn: classes) {
  description 'Assembles the jar archive that contains the JCA classes'
  from sourceSets.jca.output
  exclude 'com/gemstone/gemfire/ra/**'
  archiveName 'ra.jar'
}

task jcaJar (type: Jar, dependsOn: raJar) {
  description 'Assembles the jar archive that contains the JCA bundle'
  baseName 'gemfire-jca'
  extension 'rar'
  metaInf { from 'src/jca/ra.xml' }
  from raJar.archivePath
}

compileJava.dependsOn createVersionPropertiesFile
